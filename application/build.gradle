plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

repositories {
    jcenter()
}

configurations {
    checkstyleConfig
}

dependencies {
    testImplementation 'junit:junit:4.13'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'thorwin:thorwin.math:1.5.0'
    checkstyleConfig("com.puppycrawl.tools:checkstyle:8.32") { transitive = false }
}

mainClassName = 'pfvisualizer.ui.Main'

javafx {
    version = "12.0.2"
    modules = [ 'javafx.controls' ]
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['pfvisualizer/ui/**'])
        }))
    }
}

checkstyle {
    toolVersion '8.32'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
    ignoreFailures = false
    maxWarnings = 0
}

run {
    jvmArgs = ["--add-exports=javafx.controls/com.sun.javafx.charts=ALL-UNNAMED"]
}