plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
}

repositories {
    jcenter()
}

configurations {
    checkstyleConfig
}

dependencies {
    testImplementation 'junit:junit:4.13'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'commons-cli:commons-cli:1.4'
    checkstyleConfig("com.puppycrawl.tools:checkstyle:8.32") { transitive = false }
}

mainClassName = 'pfvisualizer.main.Main'

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['pfvisualizer/main/**'])
        }))
    }
}

checkstyle {
    toolVersion '8.32'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
    ignoreFailures = false
    maxWarnings = 0
}
